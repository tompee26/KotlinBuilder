apply plugin: 'java-library'
apply plugin: 'kotlin'
apply plugin: 'kotlin-kapt'
apply plugin: 'maven-publish'
apply plugin: 'org.jetbrains.dokka'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

sourceSets {
    generated {
        java {
            srcDirs "${buildDir}/generated/source/kapt/"
        }
    }
}

ext {
    PUBLISH_GROUP_ID = groupId
    PUBLISH_VERSION = versionCode
    PUBLISH_ARTIFACT_ID = 'kotlin-builder-compiler'
    PUBLISH_DESCRIPTION = 'Annotation processor for Kotlin Builder'
    PUBLISH_URL = publishUrl
    PUBLISH_LICENSE_NAME = license
    PUBLISH_LICENSE_URL = licenseUrl
    PUBLISH_DEVELOPER_ID = developerId
    PUBLISH_DEVELOPER_NAME = developerName
    PUBLISH_DEVELOPER_EMAIL = developerEmail
    PUBLISH_SCM_CONNECTION = scmConnection
    PUBLISH_SCM_DEVELOPER_CONNECTION = scmDevConnection
    PUBLISH_SCM_URL = scmUrl
}

dokkaJavadoc {
    dokkaSourceSets {
        configureEach {
            jdkVersion.set(8)
            noAndroidSdkLink.set(false)
        }
        named("main") {
            externalDocumentationLink {
                url = URL("https://developer.android.com/reference/")
                packageListUrl = URL("https://developer.android.com/reference/android/support/package-list")
            }
        }
    }
}

apply from: "${rootProject.projectDir}/scripts/publish-module.gradle"


Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def toolJarDir = properties.getProperty('tooljar.dir')

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation project(':annotations')
    implementation files("${toolJarDir}/lib/tools.jar")

    def incap = "0.2"
    compileOnly "net.ltgt.gradle.incap:incap:$incap"
    kapt "net.ltgt.gradle.incap:incap-processor:$incap"

    implementation "com.marcinmoskala:DiscreteMathToolkit:1.0.3"

    def kotlin_poet = "1.8.0"
    implementation "com.squareup:kotlinpoet:$kotlin_poet"
    implementation "com.squareup:kotlinpoet-metadata:$kotlin_poet"
    implementation "com.squareup:kotlinpoet-metadata-specs:$kotlin_poet"
    implementation "com.squareup:kotlinpoet-classinspector-elements:$kotlin_poet"
    implementation "com.google.auto.service:auto-service:1.0-rc6"
    kapt "com.google.auto.service:auto-service:1.0-rc6"
}