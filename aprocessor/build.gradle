apply plugin: 'java-library'
apply plugin: 'kotlin'
apply plugin: 'kotlin-kapt'
apply plugin: 'maven-publish'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

sourceSets {
    generated {
        java {
            srcDirs "${buildDir}/generated/source/kapt/"
        }
    }
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def toolJarDir = properties.getProperty('tooljar.dir')

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = project.groupProjectID
            artifactId = 'compiler'
            version = project.publishVersion
            from components.java

            pom {
                name = project.projectName
                description = 'Annotation processor for Kotlin Builder'
                url = 'https://github.com/tompee26/KotlinBuilder'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/tompee26/KotlinBuilder/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'tompee26'
                        name = 'tompee'
                        email = 'tompee26@gmail.com'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = "GithubPackages"
            url = uri("https://maven.pkg.github.com/tompee26/KotlinBuilder")
            credentials {
                username = "tompee26"
                password = properties.getProperty("token")
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation project(':annotations')
    implementation files("${toolJarDir}/lib/tools.jar")

    def incap = "0.2"
    compileOnly "net.ltgt.gradle.incap:incap:$incap"
    kapt "net.ltgt.gradle.incap:incap-processor:$incap"

    implementation "com.marcinmoskala:DiscreteMathToolkit:1.0.3"

    def kotlin_poet = "1.8.0"
    implementation "com.squareup:kotlinpoet:$kotlin_poet"
    implementation "com.squareup:kotlinpoet-metadata:$kotlin_poet"
    implementation "com.squareup:kotlinpoet-metadata-specs:$kotlin_poet"
    implementation "com.squareup:kotlinpoet-classinspector-elements:$kotlin_poet"
    implementation "com.google.auto.service:auto-service:1.0-rc6"
    kapt "com.google.auto.service:auto-service:1.0-rc6"
}